buildscript {
	repositories {
		mavenLocal()
		mavenCentral()
		maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
		jcenter()
	}
	dependencies {
	}
}

allprojects {
	apply plugin: "eclipse"
	apply plugin: "idea"

	version = "0.1.2"
	ext {
		appName = "pancake"
		eventbusVersion = "1.6.1"
		freetypeSkinVersion = "0.4"
		gdxVersion = "1.9.13"
		guacamoleVersion = "c7a8df86c1" // = 0.2.5
		reflectionsVersion = "0.11.5"
		screenmanagerVersion = "36796fc286" // = 0.6.6
		// Test dependencies:
		junitVersion = "5.6.0"
		mockitoVersion = "3.5.13"
    }

    repositories {
		mavenLocal()
		mavenCentral()
		maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
		maven { url "https://oss.sonatype.org/content/repositories/releases/" }
		maven { url "https://jitpack.io" }
		jcenter()
    }
}

project(":core") {
	apply plugin: "java-library"

	dependencies {
		implementation "com.badlogicgames.gdx:gdx:$gdxVersion"
		implementation "com.badlogicgames.gdx:gdx-freetype:$gdxVersion"
		
		implementation "com.github.crykn:guava-eventbus:$eventbusVersion"
		implementation "net.oneandone.reflections8:reflections8:$reflectionsVersion" // alternative to org.reflections:reflections, see https://github.com/ronmamo/reflections/issues/273
		api("com.github.crykn:libgdx-screenmanager:$screenmanagerVersion") {
			exclude group: "com.github.crykn.guacamole", module: "gdx" 
		}
		api( "com.github.crykn.guacamole:gdx:$guacamoleVersion")
		implementation "com.github.acanthite:freetype-skin:$freetypeSkinVersion"

		testImplementation "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
		testImplementation "org.mockito:mockito-core:$mockitoVersion"
		testImplementation "com.badlogicgames.gdx:gdx-backend-headless:$gdxVersion"
		testImplementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
		testImplementation "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-desktop"
	}

	sourceSets.main {
		resources.srcDirs = ["src/main/assets"]
	}

	sourceSets.test {
		resources.srcDirs = ["src/test/assets"]
	}
}

project(":g3d") {
	apply plugin: "java-library"

	dependencies {
		api project(":core")
		implementation "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
	}
}

project(":desktop") {
	apply plugin: "java-library"

	dependencies {
		api project(":core")
		api "com.github.crykn.guacamole:gdx-desktop:$guacamoleVersion"
		implementation "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
		
		testImplementation "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
		testImplementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
		testImplementation "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-desktop"
	}
}
