buildscript {
	repositories {
		mavenLocal()
		mavenCentral()
		maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
	}
	dependencies {
	}
}

allprojects {
	apply plugin: "eclipse"
	apply plugin: "idea"

	version = "0.1.2"
	
	ext {
		appName = "pancake"
		
		freetypeSkinVersion = "0.3.1"
		gdxVersion = "1.12.0"
		guacamoleVersion = "a2f356e397" // = 0.3.4
		reflectionsVersion = "0.11.5"
		screenmanagerVersion = "0.6.9"
		vfxVersion = "0.6.1"
		
		// Test dependencies:
		junitVersion = "5.9.3"
		mockitoVersion = "3.12.4"
    }

    repositories {
		mavenCentral()
		maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
		maven { url "https://oss.sonatype.org/content/repositories/releases/" }
		maven { url "https://jitpack.io" }
    }
}

project(":core") {
	apply plugin: "java-library"

	dependencies {
		implementation "com.badlogicgames.gdx:gdx:$gdxVersion"
		implementation "com.badlogicgames.gdx:gdx-freetype:$gdxVersion"
		
		api("com.github.crykn:libgdx-screenmanager:$screenmanagerVersion") {
			exclude group: "com.github.crykn.guacamole", module: "gdx" // we want to set our own version
		}
		api("com.github.crykn.gdx-vfx:gdx-vfx-core:$vfxVersion") {
			exclude group: "com.github.crykn.guacamole", module: "gdx" // we want to set our own version
		}
		api "com.github.crykn.guacamole:gdx:$guacamoleVersion"
		implementation "com.github.crykn:freetype-skin:$freetypeSkinVersion"

		// Test dependencies
		testImplementation "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
		testImplementation "org.mockito:mockito-core:$mockitoVersion"
		testImplementation "com.badlogicgames.gdx:gdx-backend-headless:$gdxVersion"
		testImplementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
		testImplementation "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-desktop"
	}

	sourceSets.main {
		resources.srcDirs = ["src/main/assets"]
	}

	sourceSets.test {
		resources.srcDirs = ["src/test/assets"]
	}
}

project(":g3d") {
	apply plugin: "java-library"

	dependencies {
		api project(":core")
		implementation "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
	}
}

project(":desktop") {
	apply plugin: "java-library"

	dependencies {
		api project(":core")
		api "com.github.crykn.guacamole:gdx-desktop:$guacamoleVersion"
		implementation "net.oneandone.reflections8:reflections8:$reflectionsVersion" // alternative to org.reflections:reflections, see https://github.com/ronmamo/reflections/issues/273
		implementation "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
		
		// Test dependencies
		testImplementation "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
		testImplementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
		testImplementation "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-desktop"
		testImplementation "com.github.crykn.gdx-vfx:gdx-vfx-effects:$vfxVersion"
	}
	
	sourceSets.test {
		resources.srcDirs = ["src/test/assets"]
	}
}
